plugins {
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
    id 'java'
}

group = 'com.markruler'
version = '0.1.0'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Querydsl
    // gradle dependencies --configuration compileClasspath
    implementation 'com.querydsl:querydsl-jpa' // Querydsl 라이브러리
    implementation 'com.querydsl:querydsl-apt' // Querydsl 관련 코드 생성 기능 제공

    compileOnly 'org.projectlombok:lombok'

    // 스프링 부트에 아무런 설정도 하지 않으면 h2 DB를 메모리 모드로 JVM안에서 실행한다.
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'

    // Spring Boot 2.2부터 JUnit5(jupiter)
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // SQL 실행 파라미터를 로그에 남기는 라이브러리로 개발 단계에서 사용한다.
    // 혹은 로그를 많이 남겨야 하는 시스템이면 사용할 수 있다.
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.8'
}

test {
    useJUnitPlatform()
}

///////////// Querydsl 설정 시작 /////////////
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
    library = "com.querydsl:querydsl-apt"
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', querydslDir]
        }
    }
}
configurations {
    querydsl.extendsFrom compileClasspath
}
// gradle clean compileQuerydsl -i
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}
///////////// Querydsl 설정 끝 /////////////

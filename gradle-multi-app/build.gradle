plugins {
    id 'java'
    id 'org.ec4j.editorconfig' version '0.0.3'
    id 'checkstyle'
}

// 현재 프로젝트(root)와 모든 하위 프로젝트에 적용한다.
// https://docs.gradle.org/7.5.1/dsl/org.gradle.api.Project.html#org.gradle.api.Project:allprojects(groovy.lang.Closure)
allprojects {
    repositories {
        mavenCentral()
    }
    group = 'org.xpdojo'
}

// 모든 하위 프로젝트에 적용한다.
// https://docs.gradle.org/7.5.1/dsl/org.gradle.api.Project.html#org.gradle.api.Project:subprojects(groovy.lang.Closure)
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.ec4j.editorconfig'
    apply plugin: 'checkstyle'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8


    editorconfig {
        excludes = ["build"]
    }

    check.dependsOn editorconfigCheck

    checkstyle {
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        toolVersion = "9.3"
        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

}
